; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppId={{30687ACE-03F2-4036-AF71-B760F5E8743B}
AppName={#NameLong}
AppVerName={#NameVersion}
AppPublisher=PagalSoft Inc.
AppPublisherURL=https://hlgkb.github.io/pagal-player/
AppSupportURL=https://hlgkb.github.io/pagal-player/
AppUpdatesURL=https://hlgkb.github.io/pagal-player/
DefaultDirName={pf}\{#DirName}
DefaultGroupName={#NameLong}
AllowNoIcons=yes
OutputDir={#RepoDir}\build\setup
OutputBaseFilename=PagalPlayerSetup
Compression=lzma
SolidCompression=yes
SetupIconFile={#RepoDir}\build\app.ico
UninstallDisplayIcon={app}\{#ExeBasename}.exe
ChangesEnvironment=true
ChangesAssociations=true
MinVersion=6.1.7600
SourceDir={#SourceDir}
AppVersion={#Version}
ShowLanguageDialog=auto


[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl,{#RepoDir}\build\iss\i18n\messages.en.isl"

[InstallDelete]
Type: filesandordirs; Name: {app}\lib
Type: filesandordirs; Name: {app}\modules
Type: filesandordirs; Name: {app}\worker
Type: filesandordirs; Name: {app}\node_modules
Type: files; Name: {app}\index.html
Type: files; Name: {app}\package.json
Type: files; Name: {app}\config.json

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; OnlyBelowVersion: 0,6.1
;Name: "addcontextmenufiles"; Description: "{cm:AddContextMenuFiles,{#NameShort}}"; GroupDescription: "{cm:Other}"; Flags: unchecked
;Name: "addcontextmenufolders"; Description: "{cm:AddContextMenuFolders,{#NameShort}}"; GroupDescription: "{cm:Other}"; Flags: unchecked
Name: "associatewithfiles"; Description: "{cm:AssociateWithFiles,{#NameShort}}"; GroupDescription: "{cm:Other}"; Flags: unchecked

[Files]
Source: "{#RepoDir}\build\pagal\win32\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs

[Icons]
Name: "{group}\{#NameLong}"; Filename: "{app}\{#ExeBasename}.exe"; AppUserModelID: "{#AppUserId}"
Name: "{commondesktop}\{#NameLong}"; Filename: "{app}\{#ExeBasename}.exe"; Tasks: desktopicon; AppUserModelID: "{#AppUserId}"
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#NameLong}"; Filename: "{app}\{#ExeBasename}.exe"; Tasks: quicklaunchicon; AppUserModelID: "{#AppUserId}"

[Registry]
Root: HKCR; Subkey: ".pagalist\OpenWithProgids"; ValueType: none; ValueName: "{#RegValueName}"; Flags: deletevalue uninsdeletevalue; Tasks: associatewithfiles
Root: HKCR; Subkey: ".pagalist\OpenWithProgids"; ValueType: string; ValueName: "{#RegValueName}.pagalist"; ValueData: ""; Flags: uninsdeletevalue; Tasks: associatewithfiles
Root: HKCR; Subkey: "{#RegValueName}.pagalist"; ValueType: string; ValueName: ""; ValueData: "{cm:SourceFile,pagalist}"; Flags: uninsdeletekey; Tasks: associatewithfiles
Root: HKCR; Subkey: "{#RegValueName}.pagalist\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\lib\icon\app.ico"; Tasks: associatewithfiles
Root: HKCR; Subkey: "{#RegValueName}.pagalist\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#ExeBasename}.exe"" --playlist-file ""%1"""; Tasks: associatewithfiles